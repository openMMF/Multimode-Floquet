SUBROUTINE MICROMOTIONFOURIERDRESSEDBASIS(ID,DRESSINGFIELDS_INDICES,MODES_NUM,FIELDS, U_FD,E_DRESSED,INFO)
! THIS SUBROUTINE CALCULATES THE FOURIER COMPONENTS (U_FD) AND PHASES (E_DRESSED) OF THE MICROMOTION OPERATOR OF SUBSET OF DRIVING MODES
! ID        (in)    :: TYPE(ATOM) system ID
! DRESSINGFIELDS_INDICES (in) :: integer array indicating the indices of the dressing modes
! MODES_NUM (in)    :: integer array indicating the number of harmonics of all driving modes 
! FIELDS    (in)    :: Array of TYPE(MODE) of dimension 
! U_FD      (out)   :: complex*16 matrix fourier decomposition of the micromotion operator of the dressed basis
! E_DRESSED (out)   :: dressed energies
! INFO      (inout) :: error flag
 
  USE TYPES
  IMPLICIT NONE
  TYPE(ATOM),                     INTENT(IN)  :: ID
  INTEGER,    DIMENSION(:),       INTENT(IN)  :: DRESSINGFIELDS_INDICES
  INTEGER,    DIMENSION(:),       INTENT(IN)  :: MODES_NUM
  TYPE(MODE), DIMENSION(:),       INTENT(IN)  :: FIELDS
  COMPLEX*16, DIMENSION(:,:),     ALLOCATABLE, INTENT(OUT) :: U_FD
  DOUBLE PRECISION, DIMENSION(:), ALLOCATABLE, INTENT(OUT) :: E_DRESSED
  INTEGER, INTENT(INOUT) :: INFO

  INTEGER :: m
  INTEGER :: DRESSINGFIELDS, DRESSINGFLOQUETDIMENSION

  DRESSINGFIELDS = SIZE(DRESSINGFIELDS_INDICES,1)   ! NUMBER OF DRESSING FIELDS
  DRESSINGFLOQUETDIMENSION = ID%D_BARE ! EXTENDEND DIMENSION OF THE DRESSED BASIS
  DO m=2,DRESSINGFIELDS
     DRESSINGFLOQUETDIMENSION = DRESSINGFLOQUETDIMENSION*(2*FIELDS(DRESSINGFIELDS_INDICES(m))%N_FLOQUET + 1 )
     !WRITE(*,*) m, DRESSINGFIELDS_INDICES(m),FIELDS(DRESSINGFIELDS_INDICES(m))%N_FLOQUET
  END DO
  ALLOCATE(U_FD(DRESSINGFLOQUETDIMENSION,DRESSINGFLOQUETDIMENSION))
  ALLOCATE(E_DRESSED(DRESSINGFLOQUETDIMENSION))
  CALL DRESSEDBASIS_SUBSET(ID,DRESSINGFLOQUETDIMENSION,DRESSINGFIELDS,SIZE(MODES_NUM,1),DRESSINGFIELDS_INDICES,MODES_NUM,FIELDS,&
       & U_FD,E_DRESSED,INFO) ! U_FD IS THE TRANSFORMATION OPERATOR BETWEEN THE BARE AND DRESSED BASIS, BOTH EXTENDED.

END SUBROUTINE MICROMOTIONFOURIERDRESSEDBASIS

SUBROUTINE MICROMOTIONDRESSEDBASIS(ID,MODES_NUM,DRESSINGFIELDS_INDICES,FIELDS,U_F_MODES,E_MULTIFLOQUET,T1,U,INFO) 
! THIS SUBROUTINE CALCULATES U: THE TIME-DEPENDENT MICROMOTION OPERATOR OF A SUBSET OF THE DRIVING MODES. U_F_MODES AND E_MULTIFLOQUET ARE THE ARRAYS CALCULATED WITH THE SUBROUTINE MICROMOTIONFOURIERDRESSEDBASIS

! ID (in)        :: TYPE(ATOM) system ID
! MODES_NUM (in) :: integer array indicating the number of harmonics of each driving mode
! DRESSINFIELDS_INDICES :: integer array indicating the indices of the dressing modes
! FIELDS         :: Array of TYPE(MODES) with NM components (all driving fields)
! U_F_MODES      :: complex*16 matrix of dimension DxD. Fourier decomposition of the micromotion operator of the dressed basis
! E_MULTIFLOQUET :: dressed energies
! T1             :: double precision, time
! U              :: complex*16 matrix of dimension D_BARE x D_BARE. micromotion operator at time T1
! INFO           :: error flag


  USE TYPES
  IMPLICIT NONE
  TYPE(ATOM),                       INTENT(IN)    :: ID
  INTEGER,          DIMENSION(:),   INTENT(IN)    :: MODES_NUM
  INTEGER,          DIMENSION(:),   INTENT(IN)    :: DRESSINGFIELDS_INDICES
  COMPLEX*16,       DIMENSION(:,:), INTENT(IN)    :: U_F_MODES
  DOUBLE PRECISION, DIMENSION(:),   INTENT(IN)    :: E_MULTIFLOQUET
  TYPE(MODE),       DIMENSION(:),   INTENT(IN)    :: FIELDS
  DOUBLE PRECISION ,                INTENT(IN)    :: T1
  COMPLEX*16,       DIMENSION(:,:), INTENT(OUT)   :: U
  INTEGER,                          INTENT(INOUT) :: INFO
  

  INTEGER :: r,m,NM_,TOTAL_FREQUENCIES_,FIELD_INDEX,DRESSINGFIELDS,DRESSINGFLOQUETDIMENSION
  INTEGER :: D, D_BARE

  INTEGER, DIMENSION(:), ALLOCATABLE :: MODES_NUM_
  TYPE(MODE) , DIMENSION(:), ALLOCATABLE :: FIELDS_

  
  D = SIZE(U_F_MODES,1)
  D_BARE = ID%D_BARE

  DRESSINGFIELDS = SIZE(DRESSINGFIELDS_INDICES,1)
  
  NM_ = DRESSINGFIELDS  ! NUMBER OF DRESSING MODES
  ALLOCATE(MODES_NUM_(NM_))
  DO r=1,NM_
     MODES_NUM_(r)=modes_num(DRESSINGFIELDS_INDICES(r)) ! NUMBER OF HARMONICS OF THE DRESSING MODES
  END DO
  
  TOTAL_FREQUENCIES_ = SUM(MODES_NUM_,1)  ! TOTAL NUMBER OF DRESSING FREQUENCIES
  ALLOCATE(FIELDS_(TOTAL_FREQUENCIES_))
  DO m=1,TOTAL_FREQUENCIES_
     ALLOCATE(FIELDS_(m)%V(ID%D_BARE,ID%D_BARE)) ! PREPARE SPACE TO SET THE DRESSING HAMILTONIAN COMPONENTS
  END DO

  DRESSINGFLOQUETDIMENSION = ID%D_BARE ! EXTENDEND DIMENSION OF THE DRESSED BASIS
  DO m=2,DRESSINGFIELDS
     DRESSINGFLOQUETDIMENSION = DRESSINGFLOQUETDIMENSION*(2*FIELDS(DRESSINGFIELDS_INDICES(m))%N_FLOQUET + 1 )
  END DO
  
  FIELD_INDEX = 1
  DO r=1,DRESSINGFIELDS
     DO m=1,MODES_NUM_(r)
        FIELDS_(FIELD_INDEX) = FIELDS(DRESSINGFIELDS_INDICES(r)+m-1) ! IDENTIFY THE DRESSING FIELDS FROM THE COMPLETE LIST OF FIELDS
        FIELD_INDEX = FIELD_INDEX+1
     END DO
  END DO
  
  CALL MULTIMODEMICROMOTION(ID,D,NM_,MODES_NUM_,U_F_MODES,E_MULTIFLOQUET,D_BARE,FIELDS_,T1,U,INFO) 
  
END SUBROUTINE MICROMOTIONDRESSEDBASIS


